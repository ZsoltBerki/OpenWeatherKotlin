plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id "jacoco"
    id "org.sonarqube" version "3.1.1"
}

group 'org.openweather.cached.service'
version '1.0-SNAPSHOT'

apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

ext {
    ktor_version = '1.5.3'
}

dependencies {
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // Code formatting
    ktlint "com.pinterest:ktlint:0.41.0"

    // Ktor REST client
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"

    // JSON
    implementation 'com.google.code.gson:gson:2.8.6'

    //Tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
}

// config JVM target to 1.8 for kotlin compilation tasks
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // to add a baseline to check against prepend following args:
    // "--baseline=ktlint-baseline.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

// --- TEST CONFIGURATION ---

// Using JUnit5
test {
    useJUnitPlatform()
}

// Using Jacoco to create test coverage reports
jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// Test coverage is always created after running the tests
plugins.withType(JacocoPlugin) {
    tasks["test"].finalizedBy 'jacocoTestReport'
}

// --- CODE ANALYSIS CONFIGURATION ---
sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'zsoltberki'
        property 'sonar.projectKey', 'ZsoltBerki_OpenWeatherKotlin'
        property 'sonar.sources', 'src/main/kotlin/'
    }
}